/// For MAC,INT,INC
Class sc.routines Extends sc.classes
{

/// file mask for import 
ClassMethod filemask()
{
	Q "*.MAC.xml;*.INT.xml;*.INC.xml"
}

ClassMethod exportAll() As %Status
{

    
	#; RoutineListFunc - method generated by Cache for class query RoutineList
	s rs = ##class(%Routine).RoutineListFunc() 
	s sc = $$$OK, printer = ##class(sc.printer).new()
	s handler = ..%New( "", "exportAll",  printer )
	while ( rs.%Next() ) {
		
		s handler.code = rs.Name
		
		if ( handler.ignore() ) {
			continue	
		}
		s sc = handler.export()
		Q:'sc
		
		d printer.w( handler.code_" -> "_handler.filename(), sc)
		
		
	}
	
	Q sc
}

/// Return %TimeStamp of class  
Method tsCode() As %TimeStamp
{
	s ts = ##class(%RoutineMgr).TS( ..code ) 
	Q $p( ts, "." ) ;remove milliseconds
}

/// return true for ignore code
Method ignore() As %Boolean
{
	Q:$e(..code)="%" 1 ;ignore system routines
	Q:##class(%RoutineMgr).IsGenerated( ..code ) 1 ;ignore generated routines
	Q 0
}

}

